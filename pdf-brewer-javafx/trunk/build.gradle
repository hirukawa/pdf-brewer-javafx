apply plugin: 'java'

archivesBaseName = 'pdf-brewer'
version = '0.4.3'

tasks.withType(JavaCompile) {
	sourceCompatibility = 11
	targetCompatibility = 11
	options.compilerArgs << '-Xlint:all'
}

repositories {
	jcenter()
}

dependencies {
	implementation fileTree(dir: 'lib',
			includes: ['**/*.jar'],
			excludes: ['**/*-sources.jar', '**/*-javadoc.jar'])

	implementation 'org.freemarker:freemarker:2.3.30'

	implementation 'org.apache.pdfbox:pdfbox:2.0.21'
	implementation 'org.apache.pdfbox:fontbox:2.0.21'

	implementation 'org.kordamp.ikonli:ikonli-javafx:11.5.0'
	implementation 'org.kordamp.ikonli:ikonli-materialdesign-pack:11.5.0'

	implementation 'net.osdn.util.io:auto-detect-reader:0.1.1'
	implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.14'
}

sourceSets.main.resources {
	srcDirs = ['src/main/resources', 'src/main/java' ]
}

def defaultEncoding = 'UTF-8'
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
tasks.withType(GroovyCompile).each { it.groovyOptions.encoding = defaultEncoding }

defaultTasks 'clean', 'build', 'exewrap'

jar {
	manifest {
		attributes "Specification-Version": "${project.version}"
		attributes "Main-Class" : "net.osdn.pdf_brewer.ui.Main"
		attributes "Splashscreen-Image": "img/splash.png"
		attributes "Class-Path": sourceSets.main.runtimeClasspath.collect { "lib/$it.name" }.join(' ')
	}
}

task copyLibraries(type: Copy) {
	setGroup("build")
	setDescription("Copy dependency libraries.")
	from (sourceSets.main.runtimeClasspath) {
		include '*.jar'
	}
	from('lib') {
		include '*.dll'
	}
	into "${buildDir}/package/lib/"
}

task createJavaRuntime(type: Exec, dependsOn: ['jar', 'copyLibraries']) {
	setGroup("build")
	setDescription("Create Java Runtime.")
	workingDir "${buildDir}/package/"
	commandLine 'cmd', '/c', 'CreateJRE.bat', "${jar.archiveFile.get()}", "lib", "-client"
}

task exewrap(type: Exec, dependsOn: 'jar') {
	setGroup("build")
	setDescription('Assembles the executable.')
	executable "${projectDir}/etc/exewrap.exe"
	args "-A", "x64",
		"-g",
		"-t", "11",
		"-a", "-XX:TieredStopAtLevel=1 -XX:+UseSerialGC -XX:CICompilerCount=1 -Xverify:none -Xms128m",
		"-e", "SHARED",
		"-j", "${jar.archiveFile.get()}",
		"-i", "etc/ico/app.ico",
		"-o", "${buildDir}/package/${archivesBaseName}.exe",
		"-p", "PDF BREWER",
		"-d", "PDF BREWER",
		"-c", "(C) 2017-2020 HIRUKAWA Ryo",
		"-V", "${version}",
		"-v", "${version}"
}
